buildscript {
    ext.kotlin_version = '1.2.0'
    ext.dokka_version = '0.9.15'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

plugins {
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.9.9"
    id 'org.jmailen.kotlinter' version '1.5.0'
    id "maven-publish"
}

group 'com.github.carlobellettini'
version '1.0'

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    implementation("org.assertj:assertj-assertions-generator:2.2.0") {
        exclude module: 'logback-classic'
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

dokka {
    outputFormat = "javadoc"
}

gradlePlugin {
    plugins {
        assertjGenerator {
            id = 'com.github.carlobellettini.assertj-generator-p'
            implementationClass = 'com.github.carlobellettini.gradle.assertj.plugin.AssertjGeneratorPlugin'
        }
    }
}
pluginBundle {
    website = 'https://github.com/carlovellettini/assertj-generator-gradle-plugin'
    vcsUrl = 'https://github.com/carlobellettini/assertj-generator-gradle-plugin'
    description = 'Generate Assertj assertion classes.'
    tags = ['code-generation', 'assertj', 'java']

    plugins {
        assertjGenerator {
            id = 'com.github.carlobellettini.assertj-generator-p'
            displayName = 'Assertj Generator plugin'
        }
    }
}

afterEvaluate {
    javadoc.enabled = false
    publishPluginJavaDocsJar.from(dokka)
}


compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}
